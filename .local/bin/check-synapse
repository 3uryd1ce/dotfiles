#!/usr/bin/env perl
# Copyright (c) 2022 Ashlen <eurydice@riseup.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use v5.32;
use autodie qw(:all);
use strict;
use warnings;

# Get the basename of the program we're running.
use File::Basename qw(fileparse);

# Make API requests.
use HTTP::Tiny;

# Needed for HTTP compression.
use IO::Uncompress::Gunzip qw(gunzip $GunzipError);

# Run commands simply, with detailed diagnostics.
use IPC::System::Simple qw(capturex);

# Decode JSON and manipulate it with Perl.
use JSON::MaybeXS qw(decode_json);

sub make_github_api_url {
	my $username = shift or die '$username is undefined';
	my $repository = shift or die '$repository is undefined';

	return "https://api.github.com/repos/$username/$repository/releases/latest";
}

sub make_federation_api_url {
	my $server_name = shift or die '$server_name is undefined';
	return 'https://federationtester.matrix.org/api'
		. "/report?server_name=$server_name";
}

sub decompress {
	my $response = shift or die "decompress needs a response";

	if ( exists $response->{headers}
		&& exists $response->{headers}{'content-encoding'}
		&& $response->{headers}{'content-encoding'} eq 'gzip' )
	{
		my $content = $response->{content};
		my $decompressed_content;

		gunzip \$content => \$decompressed_content,
			MultiStream => 1
			or die "gunzip: $GunzipError\n";

		$response->{content} = $decompressed_content;
	}

	return $response;
}

sub fetch_response {
	my $fetch_url = shift or die "fetch_response needs a URL.\n";
	my $http = HTTP::Tiny->new(
		verify_SSL => 1,
		default_headers => { 'Accept-Encoding' => 'gzip' }
	);
	die "TLS is unsupported: $!\n" unless $http->can_ssl;

	my $response = $http->get($fetch_url);
	die "$response->{status} $response->{reason}" unless $response->{success};

	return decompress $response;
}

sub check_dependencies {
	my $which_output = capturex qw(which --), @_;
	$which_output = undef;
}

my $program_name = fileparse $0;
my $matrix_server = shift
	or die "$program_name needs a domain name to check.\n";

my @dependencies = qw(notify-send);
check_dependencies @dependencies;

my $github_api_response =
	fetch_response( make_github_api_url qw(matrix-org synapse) );

my $decoded_github_api_response = decode_json $github_api_response->{content};
chomp( my $remote_synapse_version = $decoded_github_api_response->{'name'} );

# Exclude release candidates and catch unknown release schemes.
die "Release version did not match expected release scheme.\n"
	unless $remote_synapse_version =~ /\A v \d+\. \d+\. \d+ \z/ax;

my $federation_api_response =
	fetch_response( make_federation_api_url $matrix_server );

my $decoded_federation_api_response =
	decode_json $federation_api_response->{content};

#<<<
chomp( my $current_synapse_version =
	'v' . $decoded_federation_api_response->{'Version'}{'version'} );
#>>>

$remote_synapse_version gt $current_synapse_version
	? exec 'notify-send', "Time to upgrade to synapse $remote_synapse_version"
	: exit;
