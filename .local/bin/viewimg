#!/usr/bin/perl
# Copyright (c) 2022 Ashlen <eurydice@riseup.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use v5.32;
use autodie qw(:all);
use strict;
use warnings;

use File::Basename qw(fileparse);
use File::Temp qw(tempfile);
use HTTP::Tiny;
use IPC::System::Simple;
use URI;

my $operating_system = $^O;

my $sxiv = $operating_system eq 'openbsd' ? '/usr/local/bin/sxiv' : 'sxiv';

if ( $operating_system eq 'openbsd' ) {
	use OpenBSD::Pledge;
	use OpenBSD::Unveil;

	my %unveiled_paths = (
		'/etc/ssl/cert.pem' => 'r',   # TLS
		'/tmp' => 'rwc',   # tmpfile
		'/usr/lib' => 'r',   # libcrypto, libssl
		'/usr/libdata/perl5' => 'r',   # modules
		'/usr/local/bin/sxiv' => 'rx',   # image viewer
		'/usr/local/libdata/perl5/site_perl' => 'r',   # modules
	);

	while ( my ( $path, $permissions ) = each %unveiled_paths ) {
		unveil( $path, $permissions ) or die "Unveil failed: $!\n";
	}

	# No need for unveil anymore.
	pledge(qw(rpath tmppath fattr proc exec prot_exec dns inet))
		or die "Pledge failed: $!\n";
}

my $program_name = fileparse $0;

my $url = shift or die "$program_name needs a URL.\n";
$url =~ s/\Ahttp:/https:/;
$url = URI->new($url);
$url->scheme eq 'https'
	or die "$program_name only supports the 'https' scheme.\n";

$ENV{'DISPLAY'} // die "$program_name needs a graphical environment!\n";

chdir '/tmp';

my $http = HTTP::Tiny->new( verify_SSL => 1, );
$http->can_ssl or die "No TLS support: $!\n";

my $response = $http->get($url);

if ( $operating_system eq 'openbsd' ) {
	## No need for dns or inet anymore, as the request is stored.
	pledge(qw(rpath tmppath fattr proc exec prot_exec))
		or die "Pledge failed: $!\n";
}

$response->{success} or die "$response->{status} $response->{reason}\n";

my ( $tmp_fh, $tmpfile ) = tempfile UNLINK => 1;
open $tmp_fh, '>', $tmpfile;
print $tmp_fh $response->{content};
close $tmp_fh;

# Cannot be exec, since the temporary file needs to be cleaned up.
system $sxiv, '--', $tmpfile;

if ( $operating_system eq 'openbsd' ) {
	## No need for exec, proc, or prot_exec anymore.
	pledge(qw(rpath tmppath fattr)) or die "Pledge failed: $!\n";
}
